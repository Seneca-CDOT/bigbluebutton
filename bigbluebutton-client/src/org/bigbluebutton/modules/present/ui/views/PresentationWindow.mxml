<?xml version="1.0" encoding="utf-8"?>

<!--

BigBlueButton open source conferencing system - http://www.bigbluebutton.org/

Copyright (c) 2012 BigBlueButton Inc. and by respective authors (see below).

This program is free software; you can redistribute it and/or modify it under the
terms of the GNU Lesser General Public License as published by the Free Software
Foundation; either version 3.0 of the License, or (at your option) any later
version.

BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License along
with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.

-->

<pres:CustomMdiWindow xmlns:mx="library://ns.adobe.com/flex/mx" 
	xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:thumb="org.bigbluebutton.modules.present.views.*"
	xmlns:pres="org.bigbluebutton.common.*"
	xmlns:code="http://code.google.com/p/flexlib/" 
	xmlns:containers="flexlib.containers.*"
	xmlns:mate="http://mate.asfusion.com/"
	xmlns:views="org.bigbluebutton.modules.present.ui.views.*" 
	xmlns:poll="org.bigbluebutton.modules.polling.views.*"
	showCloseButton="false" layout="absolute"
	verticalScrollPolicy="off" 
	horizontalScrollPolicy="off"
	showControls="true" resize="resizeHandler()"
	implements="org.bigbluebutton.common.IBbbModuleWindow,org.bigbluebutton.modules.whiteboard.views.IWhiteboardReceiver"
	initialize="init()"
	creationComplete="onCreationComplete()" 
	width="{DEFAULT_WINDOW_WIDTH}" height="{DEFAULT_WINDOW_HEIGHT}" 
	x="{DEFAULT_X_POSITION}" y="{DEFAULT_Y_POSITION}"
	title="{ResourceUtil.getInstance().getString('bbb.presentation.titleWithPres',[currentPresentation])}">
	
	<fx:Declarations>
		<mate:Dispatcher id="globalDispatcher" />
		<mate:Listener type="{ShortcutEvent.FOCUS_PRESENTATION_WINDOW}" method="focusWindow" />
		<mate:Listener type="{MadePresenterEvent.SWITCH_TO_PRESENTER_MODE}" method="handleBecomePresenter" />
		<mate:Listener type="{MadePresenterEvent.SWITCH_TO_VIEWER_MODE}" method="handleBecomeViewer" />
		<mate:Listener type="{PresentationChangedEvent.PRESENTATION_CHANGED_EVENT}" method="handlePresentationChangedEvent" />
		<mate:Listener type="{UploadEvent.CLEAR_PRESENTATION}" method="clearPresentation" />
		<mate:Listener type="{DisplaySlideEvent.DISPLAY_SLIDE_EVENT}" method="handleDisplaySlideEvent" />
		<mate:Listener type="{LocaleChangeEvent.LOCALE_CHANGED}" method="localeChanged" />	
		<mate:Listener type="{ShortcutEvent.UPLOAD_PRESENTATION}" method="remoteUpload" />
		<mate:Listener type="{ShortcutEvent.PREVIOUS_SLIDE}" method="remotePrevious" />
		<mate:Listener type="{ShortcutEvent.SELECT_SLIDES}" method="remoteSelect" />
		<mate:Listener type="{ShortcutEvent.NEXT_SLIDE}" method="remoteNext" />
		<mate:Listener type="{ShortcutEvent.FIT_TO_WIDTH}" method="remoteWidth" />
		<mate:Listener type="{ShortcutEvent.FIT_TO_PAGE}" method="remotePage" />	
		<mate:Listener type="{ShortcutEvent.MINIMIZE_PRES}" method="remoteMinimize" />
		<mate:Listener type="{ShortcutEvent.MAXIMIZE_PRES}" method="remoteMaximize" />
		<mate:Listener type="{RemovePresentationEvent.UPDATE_DOWNLOADABLE_FILES_EVENT}" method="handleUpdateDownloadableFilesEvent" />
		<mate:Listener type="{PollStartedEvent.POLL_STARTED}" method="pollStartedHandler" />
		<mate:Listener type="{PollStoppedEvent.POLL_STOPPED}" method="pollStoppedHandler" />
		<mate:Listener type="{PollShowResultEvent.SHOW_RESULT}" method="pollShowResultHandler" />
	</fx:Declarations>

	<fx:Script>
		<![CDATA[
			import flash.geom.Point;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Menu;
			import mx.core.FlexGlobals;
			import mx.events.MenuEvent;
			import mx.managers.PopUpManager;
			
			import flashx.textLayout.formats.Direction;
			
			import flexlib.mdi.events.MDIWindowEvent;
			
			import org.as3commons.lang.StringUtils;
			import org.as3commons.logging.api.ILogger;
			import org.as3commons.logging.api.getClassLogger;
			import org.bigbluebutton.common.events.AddUIComponentToMainCanvas;
			import org.bigbluebutton.common.events.LocaleChangeEvent;
			import org.bigbluebutton.core.BBB;
			import org.bigbluebutton.core.KeyboardUtil;
			import org.bigbluebutton.core.Options;
			import org.bigbluebutton.core.PopUpUtil;
			import org.bigbluebutton.core.UsersUtil;
			import org.bigbluebutton.main.events.MadePresenterEvent;
			import org.bigbluebutton.main.events.ShortcutEvent;
			import org.bigbluebutton.main.views.MainCanvas;
			import org.bigbluebutton.modules.polling.events.PollShowResultEvent;
			import org.bigbluebutton.modules.polling.events.PollStartedEvent;
			import org.bigbluebutton.modules.polling.events.PollStoppedEvent;
			import org.bigbluebutton.modules.polling.events.StartPollEvent;
			import org.bigbluebutton.modules.polling.events.VotePollEvent;
			import org.bigbluebutton.modules.polling.views.PollChoicesModal;
			import org.bigbluebutton.modules.polling.views.PollResultsModal;
			import org.bigbluebutton.modules.present.commands.GoToNextPageCommand;
			import org.bigbluebutton.modules.present.commands.GoToPrevPageCommand;
			import org.bigbluebutton.modules.present.events.DisplaySlideEvent;
			import org.bigbluebutton.modules.present.events.DownloadEvent;
			import org.bigbluebutton.modules.present.events.PresentationChangedEvent;
			import org.bigbluebutton.modules.present.events.PresenterCommands;
			import org.bigbluebutton.modules.present.events.RemovePresentationEvent;
			import org.bigbluebutton.modules.present.events.UploadEvent;
			import org.bigbluebutton.modules.present.model.Page;
			import org.bigbluebutton.modules.present.model.PresentOptions;
			import org.bigbluebutton.modules.present.model.PresentationModel;
			import org.bigbluebutton.modules.whiteboard.events.RequestNewCanvasEvent;
			import org.bigbluebutton.modules.whiteboard.views.WhiteboardCanvas;
			import org.bigbluebutton.modules.whiteboard.views.WhiteboardTextToolbar;
			import org.bigbluebutton.modules.whiteboard.views.WhiteboardToolbar;
			import org.bigbluebutton.util.i18n.ResourceUtil;
             		
			private static const LOGGER:ILogger = getClassLogger(PresentationWindow);      
      
      		public static const TITLE:String = "Presentation";
			
			[Bindable] 
      		private var thumbY:Number;
			public var uploadWindow:FileUploadWindow = null;
			
			[Bindable] private var DEFAULT_X_POSITION:Number = 237;
			[Bindable] private var DEFAULT_Y_POSITION:Number = 0;
			
			private static const TOP_WINDOW_BORDER:Number = 30;
			private static const WIDTH_PADDING:Number = 8;

			[Bindable] private var DEFAULT_WINDOW_WIDTH:Number = 510;
			[Bindable] private var DEFAULT_WINDOW_HEIGHT:Number = 451;
			[Bindable] private var viewportW:Number = 451;
			[Bindable] private var viewportH:Number = 451;
			
			// Init to the size of the window.
			private var currentSlideWidth:Number = DEFAULT_WINDOW_WIDTH;
			private var currentSlideHeight:Number = DEFAULT_WINDOW_HEIGHT;
									
			private var mouseDown:Boolean = false;

			private static const PRESENTATION_NAME_MAX_LENGTH:Number = 20;
			[Bindable] private var currentPresentation:String = "";
			
			[Bindable] private var presentOptions:PresentOptions;
			
			private var keyCombos:Object;
			private var localDispatcher:Dispatcher = new Dispatcher();
			
			private var pollMenuData:Array;
			private var pollMenu:Menu;
			
			[Embed(source="../../../polling/sounds/Poll.mp3")] 
			private var noticeSoundClass:Class;
			private var noticeSound:Sound = new noticeSoundClass() as Sound;

			private var whiteboardOverlay:WhiteboardCanvas = null;
			
			private function init():void{
				presentOptions = Options.getOptions(PresentOptions) as PresentOptions;
			}
			
			private function onCreationComplete():void{
				//check for the polling module in config.xml
				var vxml:XML = BBB.getConfigForModule("PollingModule");
				if (vxml == null) fileAndPollControls.removeChild(pollStartBtn);
				
				setControlBarState("presenter");
				
				thumbY = this.height - 160;
                
				this.addEventListener(MDIWindowEvent.RESIZE_END, onResizeEndEvent);
				resourcesChanged();
				
				titleBarOverlay.tabIndex = presentOptions.baseTabIndex;
				
				hotkeyCapture();
                
        //Necessary now because of module loading race conditions
        var t:Timer = new Timer(2000, 1);
        t.addEventListener(TimerEvent.TIMER, requestWhiteboardCanvas);
        t.start();
        
        if (UsersUtil.amIPresenter()) {
          becomePresenter();
        } else {
          becomeViewer();
        }
        
        setPollMenuData();
      }
			
			private function requestWhiteboardCanvas(event:TimerEvent):void {
				LOGGER.debug("Sending event to add whiteboard canvas.");
				callLater(fitSlideToWindowMaintainingAspectRatio);
				
				var dispatcher:Dispatcher = new Dispatcher();
				dispatcher.dispatchEvent(new RequestNewCanvasEvent(this));
			}
			
			private function hotkeyCapture():void{
			    LOGGER.debug("Entering hotkeyCapture");
			    this.addEventListener(KeyboardEvent.KEY_DOWN, handleKeyDown);
			    ResourceUtil.getInstance().addEventListener(Event.CHANGE, localeChanged); // Listen for locale changing
				LOGGER.debug("Leaving hotkeyCapture");
		    }
			
			private function loadKeyCombos(modifier:String):void {
				keyCombos = new Object(); // always start with a fresh array
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.present.focusslide') as String)] = ShortcutEvent.FOCUS_SLIDE;
				
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.general.maximize') as String)] = ShortcutEvent.MAXIMIZE_PRES;
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.general.minimize') as String)] = ShortcutEvent.MINIMIZE_PRES;
				
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.present.upload') as String)] = ShortcutEvent.UPLOAD_PRESENTATION;
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.present.previous') as String)] = ShortcutEvent.PREVIOUS_SLIDE;
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.present.select') as String)] = ShortcutEvent.SELECT_SLIDES;
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.present.next') as String)] = ShortcutEvent.NEXT_SLIDE;
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.present.fitWidth') as String)] = ShortcutEvent.FIT_TO_WIDTH;
				keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.present.fitPage') as String)] = ShortcutEvent.FIT_TO_PAGE;
				//keyCombos[modifier+(ResourceUtil.getInstance().getString('bbb.shortcutkey.whiteboard.undo') as String)] = ShortcutEvent.UNDO_WHITEBOARD;

				keyCombos[Keyboard.LEFT] = ShortcutEvent.PREVIOUS_SLIDE;
				keyCombos[Keyboard.RIGHT] = ShortcutEvent.NEXT_SLIDE;
				keyCombos[Keyboard.PAGE_UP] = ShortcutEvent.PREVIOUS_SLIDE;
				keyCombos[Keyboard.PAGE_DOWN] = ShortcutEvent.NEXT_SLIDE;
			}
			
			// Handle presentation-scope hotkeys
			private function handleKeyDown(e:KeyboardEvent) :void {
				var modifier:String = ExternalInterface.call("determineModifier");
				loadKeyCombos(modifier);
				  
				var keyPress:String = KeyboardUtil.buildPressedKeys(e);
				                          
				if (keyCombos[keyPress]) {
				    //globalDispatcher.dispatchEvent(new ShortcutEvent(keyCombos[keyPress]));
				    localDispatcher.dispatchEvent(new ShortcutEvent(keyCombos[keyPress]));
				}
			}
			
			private function remoteMinimize(e:ShortcutEvent):void{
				if (!minimized){
					this.minimize();
				}
			}
			
			private function remoteMaximize(e:ShortcutEvent):void{
				if (!maximized && !minimized){
					this.maximize();
				}
				else{
					this.restore();
				}				
			}
			
			private function focusWindow(e:ShortcutEvent):void{
				if (this.visible) {
					focusManager.setFocus(titleBarOverlay);
				}
			}
			
			private function resizeHandler():void {
				// When the window is maximized, we want to resize the slide maintaining the aspect ratio.			
				fitSlideToWindowMaintainingAspectRatio();
			}
			
			private function onResizeEndEvent(event:MDIWindowEvent):void {
				fitSlideToWindowMaintainingAspectRatio();
			}
			
			private function fitSlideToWindowMaintainingAspectRatio():void {
				if (this.minimized) return;
				
				// Send the available space to display the slide.
				if (controlBar != null) {
					sendWindowResizedEvent((this.width - WIDTH_PADDING), (this.height - controlBar.height - TOP_WINDOW_BORDER));
				}
			}
			
			/*
			 * Notify the slide container telling it the available dimensions to display the slide.
			 */
			private function sendWindowResizedEvent(parentWidth:Number, parentHeight:Number):void {
				slideView.onParentResized(parentWidth, parentHeight);
			}
			
			private function handleDisplaySlideEvent(event:DisplaySlideEvent):void {		
        var curPage:Page = PresentationModel.getInstance().getCurrentPage();
        if (curPage != null) {
          displaySlideNumber(curPage.num);
          currentSlideWidth = event.slideWidth;
          currentSlideHeight = event.slideHeight;		
          fitSlideToWindowMaintainingAspectRatio();          
        }			
			}
			
			public function getPrefferedPosition():String{
				return MainCanvas.MIDDLE;
			}
						
			private function onSliderZoom():void {
				if (UsersUtil.amIPresenter()) {
					slideView.onZoomSlide(zoomSlider.value);
				}
			}
			
			private function onResetZoom():void {
				slideView.onZoomSlide(100);
			}

      private function handleBecomePresenter(e:MadePresenterEvent):void{
        becomePresenter();
      }
      
			private function becomePresenter():void{
				setupPresenter(true);
				addContextMenuItems();
			}
			
      private function handleBecomeViewer(e:MadePresenterEvent):void{
        becomeViewer();
      }
      
			private function becomeViewer():void{
				setupPresenter(false);
				dispatchEvent(new UploadEvent(UploadEvent.CLOSE_UPLOAD_WINDOW));
				this.customContextMenuItems = new Array();
				if (slideView.thumbnailView.visible)
					showThumbnails();
			}
						
			private function setupPresenter(isPresenter:Boolean):void {
				uploadPres.visible = isPresenter;
				downloadPres.visible = !isPresenter;

				var page:Page = PresentationModel.getInstance().getCurrentPage();
				if (page != null) {
					displaySlideNumber(page.num);		
				}
				displaySlideNavigationControls(isPresenter, !!page);
				
				setControlBarState("presenter");
			}
			
            private function handlePresentationChangedEvent(e:PresentationChangedEvent) : void {
				currentPresentation = PresentationModel.getInstance().getCurrentPresentationName();

                slideView.setSlides();
                slideView.visible = true;
                var page : Page = PresentationModel.getInstance().getCurrentPage();
                if (page != null) {
                    displaySlideNumber(page.num); 
                }

                if (UsersUtil.amIPresenter()) {
                    displaySlideNavigationControls(true, !!page);
                }
                else {
                    displaySlideNavigationControls(false, !!page)
                }
                onResetZoom();
                updateDownloadBtn();
            }

			
			private function displaySlideNavigationControls(isPresenter:Boolean, activePresentation:Boolean):void {
				var showButtons:Boolean = isPresenter && activePresentation;
				
				pollStartBtn.visible = showButtons;
				quickPollBtn.visible = showButtons;
				navigationControls.visible = showButtons;
				zoomControls.visible = showButtons;
				
				fitSlideToWindowMaintainingAspectRatio();
			}

			private function addContextMenuItems():void{
				var contextMenuItems:Array = new Array();
				
				var nextButton:ContextMenuItem = new ContextMenuItem(ResourceUtil.getInstance().getString('bbb.presentation.forwardBtn.toolTip'));
				nextButton.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, function(e:ContextMenuEvent):void { goToNextSlide(); });
				contextMenuItems.push(nextButton);
				
				var previousButton:ContextMenuItem = new ContextMenuItem(ResourceUtil.getInstance().getString('bbb.presentation.backBtn.toolTip'));
				previousButton.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, function(e:ContextMenuEvent):void { goToPreviousSlide(); });
				contextMenuItems.push(previousButton);
								
				this.customContextMenuItems = contextMenuItems;
			}
			
			private function notifyOthersOfSharingPresentation(presentationName:String):void {
				var shareEvent:PresenterCommands = new PresenterCommands(PresenterCommands.SHARE_PRESENTATION_COMMAND);
					shareEvent.presentationName = presentationName;
					dispatchEvent(shareEvent);
			}
			
			private function disableSlideNavigationButtons(pageNumber:int):void {
				var styleNameExt : String = "";
				if (ResourceUtil.getInstance().isRTLEnabled() && ResourceUtil.getInstance().getCurrentLanguageDirection() == Direction.RTL) {
					styleNameExt = "RTL";
				}

				// We use mouseEnabled here instead of enabled because the tab position gets lost when going through 
				//the pages. Please don't change this if you don't know why this is.
				if (pageNumber != 1) {
					backButton.mouseEnabled = true;
					backButton.styleName = "presentationBackButtonStyle" + styleNameExt;
				} else {
					backButton.mouseEnabled = false;
					backButton.styleName = "presentationBackButtonDisabledStyle" + styleNameExt;
				}
				
				if (pageNumber < PresentationModel.getInstance().getNumberOfPages()) {
					forwardButton.mouseEnabled = true;
					forwardButton.styleName = "presentationForwardButtonStyle" + styleNameExt;
				} else {
					forwardButton.mouseEnabled = false;
					forwardButton.styleName = "presentationForwardButtonDisabledStyle" + styleNameExt;
				}
			}
			
			private function displaySlideNumber(currentSlide:int):void {
        		disableSlideNavigationButtons(currentSlide);
				btnSlideNum.label = "" + currentSlide + '/' + PresentationModel.getInstance().getNumberOfPages();
				btnSlideNum.accessibilityName = ResourceUtil.getInstance().getString('bbb.presentation.btnSlideNum.accessibilityName', [currentSlide, PresentationModel.getInstance().getNumberOfPages()])
			}
			
      
			private function clearPresentation(e:UploadEvent):void{
				slideView.visible = false;
				slideView.slideLoader.source = null; 
				slideView.setSelectedSlide(0);
				btnSlideNum.label = "";
				
				pollStartBtn.visible = false;
				quickPollBtn.visible = false;

				navigationControls.visible = false;
				zoomControls.visible = false;

				// Change the title of the window.
				currentPresentation = ResourceUtil.getInstance().getString('bbb.presentation.title');				
			}
			
			private function goToPreviousSlide():void {
				var curPage:Page = PresentationModel.getInstance().getCurrentPage();
				if (curPage != null && curPage.num > 1) {
					dispatchEvent(new GoToPrevPageCommand());
				}
			}
			
			private function goToNextSlide():void {
				var curPage:Page = PresentationModel.getInstance().getCurrentPage();
				if (curPage != null && curPage.num < PresentationModel.getInstance().getNumberOfPages()) {
					dispatchEvent(new GoToNextPageCommand());
				}
			}
			
      private function showPercentageInDataTip(val:String):String {			
        return val + "%"; 
      }
      
      private function showThumbnails():void{
        slideView.thumbnailView.visible = !slideView.thumbnailView.visible;
      }

      private function addWhiteboardCanvasToSlideView():void{
        if(whiteboardOverlay != null) {
          LOGGER.debug("addWhiteboardCanvasToSlideView: Adding whiteboard canvas to SlideView");
          slideView.acceptOverlayCanvas(whiteboardOverlay);
		}
      }
			
			override protected function resourcesChanged():void{
				super.resourcesChanged();
				
				if (titleBarOverlay != null) {
					titleBarOverlay.accessibilityName = ResourceUtil.getInstance().getString('bbb.presentation.titleBar');
				}
				
				if (windowControls != null) {
					minimizeBtn.toolTip = ResourceUtil.getInstance().getString("bbb.window.minimizeBtn.toolTip");
					minimizeBtn.accessibilityName = ResourceUtil.getInstance().getString("bbb.presentation.minimizeBtn.accessibilityName");

					maximizeRestoreBtn.toolTip = ResourceUtil.getInstance().getString("bbb.window.maximizeRestoreBtn.toolTip");
					maximizeRestoreBtn.accessibilityName = ResourceUtil.getInstance().getString("bbb.presentation.maximizeRestoreBtn.accessibilityName");

					closeBtn.toolTip = ResourceUtil.getInstance().getString("bbb.window.closeBtn.toolTip");
					closeBtn.accessibilityName = ResourceUtil.getInstance().getString("bbb.presentation.closeBtn.accessibilityName");
				}
				
				if(Capabilities.hasAccessibility)
					Accessibility.updateProperties();

				if (UsersUtil.amIPresenter()) {
					addContextMenuItems();
				} else {
					this.customContextMenuItems = new Array();
				}
				
				setPollMenuData();
				
				updateStyles();
			}
			
			private function localeChanged(e:Event):void{
				resourcesChanged();
			}
			
			private function onFitToPage(ftp:Boolean):void {
				slideView.switchToFitToPage(ftp);
				fitSlideToWindowMaintainingAspectRatio();				
			}
			
			
			override protected function hideAllChildren():void {
				slideView.includeInLayout = false;
			}
			
			override protected function showAllChildren():void {
				slideView.includeInLayout = true;
			}
			
			private function remoteUpload(e:ShortcutEvent):void{
				if (uploadPres.visible){
					uploadPres.setFocus();
					openUploadWindow();
				}
			}
			
			private function remotePrevious(e:ShortcutEvent):void{
				if (backButton.visible){
					//backButton.setFocus();
					goToPreviousSlide();
				}
			}
			
			private function remoteSelect(e:ShortcutEvent):void{
				if (btnSlideNum.visible){
					btnSlideNum.setFocus();
					showThumbnails();
				}
			}
			
			private function remoteNext(e:ShortcutEvent):void{
				if (forwardButton.visible){
					//forwardButton.setFocus();
					goToNextSlide();
				}
			}
			
			private function remoteWidth(e:ShortcutEvent):void{
				if (btnFitToWidth.visible){
					btnFitToWidth.setFocus();
					onFitToPage(false);
				}
			}
			
			private function remotePage(e:ShortcutEvent):void{
				if (btnFitToPage.visible){
					btnFitToPage.setFocus();
					onFitToPage(true);
				}
			}

			private function onDownloadButtonClicked():void {
				openDownloadWindow();
			}

			private function openDownloadWindow():void {
				var event:DownloadEvent = new DownloadEvent(DownloadEvent.OPEN_DOWNLOAD_WINDOW);
				dispatchEvent(event);
			}
			
			private function onUploadButtonClicked():void {
				openUploadWindow();	
			}

			private function openUploadWindow():void {
				if (presentOptions.openExternalFileUploadDialog) {
					var fileEvent:UploadEvent = new UploadEvent(UploadEvent.OPEN_EXTERNAL_UPLOAD_WINDOW);
					fileEvent.maxFileSize = presentOptions.maxFileSize;
					var gDispatcher:Dispatcher = new Dispatcher();
					gDispatcher.dispatchEvent(fileEvent);
				} else {
					var event:UploadEvent = new UploadEvent(UploadEvent.OPEN_UPLOAD_WINDOW);
					event.maxFileSize = presentOptions.maxFileSize;
					dispatchEvent(event);
				}
			}

			private function quickPollClicked(e:MouseEvent):void {
				sendStartPollEvent(Button(e.target).name);
			}
			
			private function setPollMenuData():void {
				var resourceUtil:ResourceUtil = ResourceUtil.getInstance();
				pollMenuData = [];
				pollMenuData.push({label: resourceUtil.getString("bbb.polling.answer.Yes") + "/" + resourceUtil.getString("bbb.polling.answer.No")});
				pollMenuData.push({label: resourceUtil.getString("bbb.polling.answer.True") + "/" + resourceUtil.getString("bbb.polling.answer.False")});
				var constructedLabel:String = ResourceUtil.getInstance().getString("bbb.polling.answer."+String.fromCharCode(65));
				for (var i:int=1; i<6; i++) {
					constructedLabel += "/" + ResourceUtil.getInstance().getString("bbb.polling.answer."+String.fromCharCode(65+i));
					pollMenuData.push({label: constructedLabel});
				}
				pollMenuData.push({label: resourceUtil.getString("bbb.polling.customPollOption.label")});
			}

			private function updateStyles():void {
				if (PresentationModel.getInstance().getCurrentPage()) {
					disableSlideNavigationButtons(PresentationModel.getInstance().getCurrentPage().num);
				}
			}

			private function onPollStartButtonClicked():void {
				openPollTypeMenu();
			}
			
			private function openPollTypeMenu():void {
				// make sure the previous menu is closed before opening a new one
				// This could be improved to include a flag that tells if the menu is open,
				// but it would require an extra listener for the MenuCloseEvent.
				if (pollMenu) {
					pollMenu.removeEventListener(MenuEvent.ITEM_CLICK, menuClickHandler);
					pollMenu.removeEventListener(MenuEvent.MENU_SHOW, menuShowHandler);
					pollMenu.hide();
				}
				
				pollMenu = Menu.createMenu(null, pollMenuData, true);
				pollMenu.addEventListener(MenuEvent.ITEM_CLICK, menuClickHandler);
				pollMenu.addEventListener(MenuEvent.MENU_SHOW, menuShowHandler);
				pollMenu.variableRowHeight = false;
				var pollXY:Point = pollStartBtn.localToGlobal(new Point(pollStartBtn.width + 2, pollStartBtn.height - pollMenu.height));
				//pollMenu.show(this.x + pollStartBtn.x + pollStartBtn.width + 2, this.y + this.height);
				pollMenu.show();

			}

            private function sendStartCustomPollEvent(pollType:String):void {
                // Let's reset the page to display full size so we can display the result
                // on the bottom right-corner.
                var pollChoicesPopUp:PollChoicesModal = PopUpUtil.createModalPopUp(FlexGlobals.topLevelApplication as DisplayObject, PollChoicesModal, true) as PollChoicesModal;
                if (pollChoicesPopUp) {
                    pollChoicesPopUp.setPresentationWindow(this);
                }
            }

			private function sendStartPollEvent(pollType:String):void {
				// Let's reset the page to display full size so we can display the result
				// on the bottom right-corner.
				onResetZoom();
				var dispatcher:Dispatcher = new Dispatcher();
				dispatchEvent(new StartPollEvent(pollType));				
			}
			
			private function menuClickHandler(e:MenuEvent):void {
				if(pollMenuData[e.index] != undefined) {
					// start the requested poll
					switch (e.index) {
						case 0:
							sendStartPollEvent("YN");
							break;
						case 1:
							sendStartPollEvent("TF");
							break;
						case 2:
							sendStartPollEvent("A-2");
							break;
						case 3:
							sendStartPollEvent("A-3");
							break;
						case 4:
							sendStartPollEvent("A-4");
							break;
						case 5:
							sendStartPollEvent("A-5");
							break;
						case 6:
							sendStartPollEvent("A-6");
							break;
						case 7:
							sendStartCustomPollEvent("Custom");
							break;
						default:
							LOGGER.warn("Wrong poll menu selected index {0}", [e.index]);
							break;
					}

				}
			}
			
			private function menuShowHandler(e:MenuEvent):void {
				pollMenu.setFocus();
				var pollXY:Point = pollStartBtn.localToGlobal(new Point(pollStartBtn.width + 2, pollStartBtn.height - pollMenu.height));
				pollMenu.x = pollXY.x;
				pollMenu.y = pollXY.y;
			}
			
			private function pollStartedHandler(e:PollStartedEvent):void {
				// the event for this doesn't exist yet
				if (UsersUtil.amIPresenter()) {
					// display the results view
					var pollResultsPopUp : PollResultsModal = PopUpUtil.createModalPopUp(FlexGlobals.topLevelApplication as DisplayObject, PollResultsModal, true) as PollResultsModal;
					if (pollResultsPopUp) {
						pollResultsPopUp.setPoll(e.poll);
					}
					PopUpManager.centerPopUp(pollResultsPopUp);
				} else {
					//switch to vote state
					setControlBarState("vote");
					//clear the past buttons
					pollVoteBox.removeAllChildren();
					//display the vote buttons
					var voteBtn:Button;
					var numBtns:int = e.poll.answers.length;
					var btnWidth:int = 100/numBtns;
					for (var i:int=0, ti:int=slideView.tabIndex; i<numBtns; i++) {
							voteBtn = new Button();
							voteBtn.percentWidth = btnWidth;
							voteBtn.height = 70;
							voteBtn.setStyle("fontSize", 14);
							voteBtn.tabIndex = ++ti;
							
							var localizedKey: String = ResourceUtil.getInstance().getString('bbb.polling.answer.'+e.poll.answers[i].key);

							if (StringUtils.isEmpty(localizedKey) || localizedKey == "undefined") {
								localizedKey = e.poll.answers[i].key;
							} 
							
							voteBtn.label = localizedKey;
							voteBtn.name = e.poll.answers[i].id;
							voteBtn.addEventListener(MouseEvent.CLICK, voteButtonClickHandler);
							pollVoteBox.addChild(voteBtn);
					}
					
					var tSC:SoundChannel = noticeSound.play(0, 0, new SoundTransform(0.25));
				}
			}
			
			private function voteButtonClickHandler(e:MouseEvent):void {
				//still have to figure out how to determine which button was pressed
				LOGGER.debug("vote clicked - answer is {0}", [e.target.name]);
				setControlBarState("presenter");
				dispatchEvent(new VotePollEvent(Number(e.target.name)));
			}
			
			private function setControlBarState(state:String):void {
				if (state == "vote") {
					fileAndPollControls.visible = fileAndPollControls.includeInLayout= false;
					navigationControls.visible = navigationControls.includeInLayout= false;
					zoomControls.visible = zoomControls.includeInLayout= false;
					pollVoteBox.visible = pollVoteBox.includeInLayout = true;
					
				} else if (state == "presenter" && UsersUtil.amIPresenter()) {
					pollStartBtn.visible = true;

					fileAndPollControls.visible = fileAndPollControls.includeInLayout= true;
					navigationControls.visible = navigationControls.includeInLayout= true;
					zoomControls.visible = zoomControls.includeInLayout= true;
					pollVoteBox.visible = pollVoteBox.includeInLayout = false;
				} else {
					pollVoteBox.visible = pollVoteBox.includeInLayout = false;
					pollStartBtn.visible = false;

					fileAndPollControls.visible = fileAndPollControls.includeInLayout= true;
					navigationControls.visible = navigationControls.includeInLayout= false;
					zoomControls.visible = zoomControls.includeInLayout= false;
				}
				
				// Need to call the function later because the heights haven't been validated yet
				callLater(fitSlideToWindowMaintainingAspectRatio);
			}
			
			private function pollStoppedHandler(e:PollStoppedEvent):void {
				setControlBarState("presenter");
				PopUpUtil.removePopUp(PollResultsModal);
			}
			
			private function pollShowResultHandler(e:PollShowResultEvent):void {
				setControlBarState("presenter");
			}

			private function handleUpdateDownloadableFilesEvent(e:RemovePresentationEvent):void {
				updateDownloadBtn();
			}

			private function updateDownloadBtn():void {
				if (downloadPres == null) {
					return;
				}

				if (pollVoteBox.visible) {
					downloadPres.visible = false;
					return;
				}

				var downloadablePresentations:ArrayCollection = PresentationModel.getInstance().getDownloadablePresentations();
				if (presentOptions.enableDownload && downloadablePresentations.length > 0 && !UsersUtil.amIPresenter()) {
					LOGGER.debug("Enabling download presentation button. There are {0} presentations available for downloading.", [downloadablePresentations.length]);
					downloadPres.visible = true;
				} else {
					LOGGER.debug("Disabling download presentation button. There are {0} presentations available for downloading.", [downloadablePresentations.length]);
					downloadPres.visible = false;
				}
			}

			public function receiveToolbars(wt:WhiteboardToolbar, wtt:WhiteboardTextToolbar):void {
				var addUIEvent:AddUIComponentToMainCanvas = new AddUIComponentToMainCanvas(AddUIComponentToMainCanvas.ADD_COMPONENT);
				addUIEvent.component = wt;
				globalDispatcher.dispatchEvent(addUIEvent);
				wt.positionToolbar(this);
				
				var addTextToolbarEvent:AddUIComponentToMainCanvas = new AddUIComponentToMainCanvas(AddUIComponentToMainCanvas.ADD_COMPONENT);
				addTextToolbarEvent.component = wtt;
				globalDispatcher.dispatchEvent(addTextToolbarEvent);
				wtt.positionToolbar(this);
			}
			
			public function receiveCanvas(wc:WhiteboardCanvas):void {
				whiteboardOverlay = wc;
				
				addWhiteboardCanvasToSlideView();
			}
		]]>
	</fx:Script>

	<fx:Declarations>
		<pres:TabIndexer startIndex="{presentOptions.baseTabIndex + 1}"
				tabIndices="{[minimizeBtn, maximizeRestoreBtn, closeBtn, slideView.slideLoader, uploadPres, pollStartBtn, quickPollBtn, downloadPres, backButton, btnSlideNum, forwardButton, zoomSlider, btnFitToWidth, btnFitToPage]}"/>
	  	 
		<mx:Fade id="thumbFadeIn" alphaFrom="1" alphaTo="0" duration="100" />
		<mx:Fade id="thumbFadeOut" alphaFrom="0" alphaTo="1" duration="100" />
	</fx:Declarations>

	<mx:Canvas width="100%" height="100%" styleName="slideViewBackground" verticalScrollPolicy="off" horizontalScrollPolicy="off">
		<views:SlideView id="slideView" width="100%" height="100%" visible="false" mouseDown="mouseDown = true" mouseUp="mouseDown = false"/>
	</mx:Canvas>

	<mx:ControlBar id="presCtrlBar" name="presCtrlBar" width="100%" verticalAlign="top" styleName="presentationWindowControlsStyle" paddingTop="6" paddingBottom="6">
		<!-- Presentation Actions -->
		<mx:HBox id="fileAndPollControls" width="40%" height="100%"
				 horizontalScrollPolicy="off" verticalAlign="bottom" horizontalAlign="left">
			<mx:Button id="uploadPres" includeInLayout="{uploadPres.visible}"
					   visible="false" height="30" styleName="presentationUploadButtonStyle"
					   toolTip="{ResourceUtil.getInstance().getString('bbb.presentation.uploadPresBtn.toolTip')}"
					   click="onUploadButtonClicked()"/>
			<mx:Button id="pollStartBtn" visible="false" height="30" styleName="pollStartButtonStyle"
					   toolTip="{ResourceUtil.getInstance().getString('bbb.polling.startButton.tooltip')}"
					   click="onPollStartButtonClicked()" includeInLayout="{pollStartBtn.visible}"/>
			<poll:QuickPollButton id="quickPollBtn" height="30"
								  click="quickPollClicked(event)"
								  includeInLayout="{quickPollBtn.visible}" />
			<mx:Button id="downloadPres" visible="false"
					   includeInLayout="{downloadPres.visible}" height="30"
					   styleName="presentationDownloadButtonStyle"
					   toolTip="{ResourceUtil.getInstance().getString('bbb.presentation.downloadPresBtn.toolTip')}"
					   click="onDownloadButtonClicked()" creationComplete="updateDownloadBtn()"/>
		</mx:HBox>

		<mx:HBox id="navigationControls" width="30%" height="100%" paddingTop="0"
				 visible="false" includeInLayout="{navigationControls.visible}" horizontalAlign="center" verticalAlign="middle">
			<mx:Button id="backButton" height="30" styleName="presentationBackButtonStyle"
					   toolTip="{ResourceUtil.getInstance().getString('bbb.presentation.backBtn.toolTip')}" click="goToPreviousSlide()"/>
			<mx:Button id="btnSlideNum" height="30" click="showThumbnails()"
					   toolTip="{ResourceUtil.getInstance().getString('bbb.presentation.btnSlideNum.toolTip')}"/>
			<mx:Button id="forwardButton" height="30" styleName="presentationForwardButtonStyle"
					   toolTip="{ResourceUtil.getInstance().getString('bbb.presentation.forwardBtn.toolTip')}" click="goToNextSlide()"/>
		</mx:HBox>

		<mx:HBox id="zoomControls" width="40%" height="100%"
				 horizontalScrollPolicy="off" verticalAlign="middle" horizontalAlign="right"
				 visible="false" includeInLayout="{zoomControls.visible}">
			<mx:HSlider id="zoomSlider" value="{slideView.zoomPercentage}" styleName="presentationZoomSliderStyle"
						minimum="100" maximum="400" dataTipPlacement="top" 
						useHandCursor="true" snapInterval="5" allowTrackClick="true" liveDragging="true" 
						dataTipFormatFunction="showPercentageInDataTip" change="onSliderZoom()" width="100"
						accessibilityName="{ResourceUtil.getInstance().getString('bbb.presentation.slider')}"/>
			<mx:Button id="btnFitToWidth" height="30" styleName="presentationFitToWidthButtonStyle"
					   toolTip="{ResourceUtil.getInstance().getString('bbb.presentation.fitToWidth.toolTip')}" 
					   click="onFitToPage(false)"/>
			<mx:Button id="btnFitToPage" height="30" styleName="presentationFitToPageButtonStyle"
					   toolTip="{ResourceUtil.getInstance().getString('bbb.presentation.fitToPage.toolTip')}" 
					   click="onFitToPage(true)"/>
		</mx:HBox>
		<mx:VBox width="100%" height="100%" visible="{pollVoteBox.visible}" includeInLayout="{pollVoteBox.includeInLayout}" horizontalAlign="center">
			<mx:Label id="pollVoteLabel" styleName="pollVoteLabel" text="{ResourceUtil.getInstance().getString('bbb.presentation.poll.response')}"/>
			<mx:HBox id="pollVoteBox" width="100%" height="100%" visible="false" includeInLayout="false" horizontalAlign="center" verticalAlign="middle"/>	
		</mx:VBox>
	</mx:ControlBar>
</pres:CustomMdiWindow>
